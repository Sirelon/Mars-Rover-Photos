apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-kapt'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.sirelon.marsroverphotos"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 19
        versionName "1.4.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            multiDexEnabled false
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
        }
        debug {
            multiDexEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.3.21'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testImplementation 'org.amshove.kluent:kluent:1.29'

    // https://developer.android.com/jetpack/androidx/releases/
    implementation "androidx.activity:activity-ktx:1.1.0-rc02"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta3"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.fragment:fragment-ktx:1.2.0-rc02"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-rc02'

    def room_version = "2.2.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'androidx.paging:paging-runtime:2.1.0'
    // https://github.com/material-components/material-components-android/releases
    implementation "com.google.android.material:material:1.2.0-alpha01"

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    // Pinch Zoom Library
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'

    def retrofitVer = '2.6.1'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVer"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVer"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVer"

    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true;
    }

    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-ads:18.3.0'
    implementation 'com.google.android.gms:play-services-ads:18.3.0'
    implementation 'com.google.firebase:firebase-crash:16.2.1'
    implementation 'com.google.firebase:firebase-firestore:21.3.0'
    implementation 'com.google.firebase:firebase-perf:19.0.2'

    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'
}
apply plugin: 'com.google.gms.google-services'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    google()
}