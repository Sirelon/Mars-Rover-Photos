//@Suppress("DSL_SCOPE_VIOLATION")
plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.ksp)
    id 'kotlin-parcelize'
    alias(libs.plugins.google.services)
    alias(libs.plugins.firebase.perf)
    alias(libs.plugins.firebase.crashlytics)
    alias(libs.plugins.compose.compiler)
    id("io.gitlab.arturbosch.detekt")
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 36
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.sirelon.marsroverphotos"
        minSdkVersion 23
        targetSdkVersion 36
        versionCode 47
        versionName "2.5.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }
    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
        }
        debug {
        }
    }

    configurations{
        all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
        buildConfig true
    }

    namespace 'com.sirelon.marsroverphotos'
}

ksp {
    arg("room.generateKotlin", "true")
    arg("room.schemaLocation", "$projectDir/schemas")
}

dependencies {
    implementation(libs.androidx.ktx)
    implementation(libs.ksp)
    implementation(libs.androidx.compose)

    debugImplementation(libs.compose.ui.tooling)
    implementation(libs.compose.ui)
    implementation(libs.compose.ui.util)
    implementation(libs.compose.foundation)
    implementation(libs.compose.material3)
    implementation(libs.compose.material3.adaptive.navigation.suite)
    implementation(libs.compose.ui.tooling)

    implementation libs.navigation.compose

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation libs.kotlin.stdlib
    testImplementation libs.kotlin.test.junit
    testImplementation libs.mockito.kotlin

    implementation libs.androidx.activity.compose
    implementation libs.androidx.annotation

    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    implementation libs.androidx.room.paging
    ksp libs.androidx.room.compiler

    implementation libs.androidx.work.runtime.ktx

    implementation libs.androidx.paging.runtime.ktx
    implementation libs.androidx.paging.compose

    implementation(libs.coil)
    implementation(libs.coil.network)
    implementation libs.androidx.appcompat

    implementation libs.ktor.core
    implementation libs.ktor.client.okhttp
    implementation libs.ktor.client.content.negotiation
    implementation libs.ktor.serialization.kotlinx.json

    implementation libs.kotlinx.serialization.json

    implementation(platform(libs.firebase.bom))
    implementation(libs.bundles.firebase)

    implementation libs.play.services.ads
    implementation libs.user.messaging.platform

    implementation libs.timber

    implementation(libs.kotlinx.collections.immutable)

    implementation libs.zoomable
}

if (!file("$projectDir/google-services.json").exists()) {
    tasks.matching { it.name == "processDebugGoogleServices" }.configureEach {
        enabled = false
    }
}
